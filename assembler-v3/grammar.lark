start: statement+

?statement: code_block
    | data_block
    | constantdef
    | dot_directive

code_block: "func" IDENTIFIER "{" code_statement* "}"

?code_statement: constantdef
    | labeldef
    | instruction

instruction: IDENTIFIER (parameter ("," parameter)?)? "\n"

?parameter: register
    | immediate
    | address

register: REGISTER
immediate: expr
?address: "[" [ADDR_SIZE] register ["+" expr] "]" -> indirect_addr
    | "[" [ADDR_SIZE] expr "]" -> direct_addr

data_block: "data " [IDENTIFIER] "{" data_statement* "}"

?data_statement: dot_directive
    | constantdef
    | labeldef

constantdef: "const" IDENTIFIER "=" expr
labeldef: IDENTIFIER ":"

?dot_directive: ".ascii" STRING -> text
    | ".asciiz" STRING -> text_nulterm
    | ".byte" expr -> byte
    | ".word" expr -> word
    | ".double" expr -> double
    | ".quad" expr -> quad
    | ".zero" expr -> zero
    | ".org" expr -> org

?expr: bitwise_or

?bitwise_or: bitwise_xor
    | bitwise_or "|" bitwise_xor -> or_op

?bitwise_xor: bitwise_and
    | bitwise_xor "^" bitwise_and -> xor_op

?bitwise_and: shift
    | bitwise_and "&" shift -> and_op

?shift: sum
    | shift ">>" sum -> shiftr
    | shift "<<" sum -> shiftl

?sum: term
    | sum "+" term -> add
    | sum "-" term -> sub

?term: unary
    | term "*" unary -> mul
    | term "/" unary -> div
    | term "%" unary -> mod

?unary: factor
    | "~" unary -> not_op
    | "-" unary -> unary_sub

?factor: "(" expr ")"
    | symbol
    | literal

literal: DECIMAL
    | "0b" BINARY
    | "0o" OCTAL
    | "0x" HEX
    | CHAR

symbol: IDENTIFIER

// terminals
IDENTIFIER: /[a-zA-Z_][a-zA-Z0-9_]*/
COMMENT: ";" /[^\n]*/

REGISTER: "a"i | "x"i | "y"i
    |"bp"i
    |"iv"i
    |"ip"i
    |"sp"i

ADDR_SIZE: "b"|"w"|"d"|"q"

DECIMAL: /[0-9_]+/
BINARY: /[01_]+/
OCTAL: /[0-7_]+/
HEX: /[0-9a-fA-F_]+/
STRING: ESCAPED_STRING

CHAR: "'" /.*?/ /(?<!\\)(\\\\)*?/ "'"

%import common.ESCAPED_STRING
%import common.WS

%ignore WS
%ignore COMMENT
